$registeredMobileTools ? ModifiableList = ().
$registerMobileTool = ($toolFactory ; $_label ? ("Needs a label" default String)) fun {
	($push of registeredMobileTools): [toolFactory;_label].
}.

#{
	Generates a toolbar overlay with a standardized layout for mobile devices (usually smartphones).
	This bar is only visible when the app window is soo small for desktop controls, otherwise it is hidden.
	
	This toolbar is empty by default, except for two buttons on the right ("user" and "apps") and one on the left ("menu").
	
	To add custom tools for an app, use "registerMobileTool".
	That will not actually add a tool to the bar itself, but it will add an option that appears when the user taps the menu button.
	For more information about adding desktop tools, see the documentation of "registerDesktopTool".
	
	Please note that if you are making an app for mobile and desktop, you sould use "uiGlobalToolbar" instead of this function.
	It takes care of adding all necessary UI elements to make the toolbar look nice.
}
$generateMobileToolbarOverlay = () fun {
	$mobileTools = getArray: registeredMobileTools.
	(($_tool) fun {
		$popupRef = reference: $popup.
		$toolName = "tool_" + urlEncodeParameter: second: _tool.
		
		toolName registerSubApp {
			$view = do: first: _tool.
			!if (not view eq void) {
				uiShowPopup: popupRef = (do: first: _tool) uiPopup {openSubApp: ""}
			}. !else closeSubApp
		};{}
	}) each mobileTools.
	
	$tools registerSubApp popupSubApp: {
		(
			(uiTitleToolbar: "Tools");
			(
				@verticalScroller @center @(percentWidth: 95) @(height "95% - " + toolbarCmHeight + "cm") {
					openSubApp: $id objFirstProperty calcitAnnotations: x
				} uiSingleViewPicker {
					@("id";"tool_" + urlEncodeParameter: second: x) @(percentWidth: 100) uiContainer: second: x
				} each mobileTools
			)
		)
	}.

	@(cssClass: "k-mobile_only") uiToolbarOverlay:
		[
			@dynamicSize @(percentHeight 75) @(width: (toolbarCmHeight * 0,75) + "cm + 7px") @verticalCenter @left uiContainer:
				[uiHorizontalPxSpace: 7];
				[@(cmWidth toolbarCmHeight * 0,75) uiHamburgerButton: { openSubApp: $tools }]
		];
		`Add the default navigation buttons (uiUserButton and uiAppsButton) at the right`
		[
			@verticalCenter @right @(percentHeight 75) @(cmWidth: (toolbarCmHeight * 1,5) + 0,7) uiContainer:
				[@(cmWidth toolbarCmHeight * 0,75) !uiAppsButton];
				[@(cmWidth 0,4) uiContainer:void];
				[@(cmWidth toolbarCmHeight * 0,75) !uiUserButton];
				[uiHorizontalPxSpace: 7]
		].
}.