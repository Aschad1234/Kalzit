`A simple image gallery for instagram, reddit and youtube`

$scrollers ? (List:Any) = void.
$scrollersRef = reference: $scrollers.
$users ? (List:String) = "".
$usersRef = reference: $users.

$youtubeUserVideoThumbnailsAndUrls = ($user ? String) fun {{(youtubeVideoUrl+x);youtubeVideoThumbnail: x} each youtubeUserVideoIds: user} ? (List:2 SizedList Url).

$uiPreparePopup = {@(percentWidth 80) @(percentHeight 80) @(backgroundColor white) x}.
$standardImageView = ($index ; $_otherImageUrls) fun {
	$_urls = index at _otherImageUrls.
	@(percentRoundness 10) @(pxWidth 200) @(pxHeight 200) @(onTap () fun {
		print: uiPopup: ({
			@(onTap () fun {popupUrl: first: x}) @center @fit uiShowImageUrl: second: x
		} each _otherImageUrls) uiSlidesWithNavigation index
	}) $view = uiShowImageUrl: second: _urls
}.

$scrollerForTerm = ($term ? String) fun {
	$urlPairs = do: first: term switch
		[{"#" strStartsWith term};{instagramTagImages: "#" strRemove term}];
		[{"r/" strStartsWith term};{redditFeedEntryUrlsWithImages: "r/" strRemove term}];
		[{"ytu/" strStartsWith term};{youtubeUserVideoThumbnailsAndUrls: "ytu/" strRemove term}];
		[true;{instagramUserImages: term}].
	$imageUrls = second each urlPairs.
	uiContainer: (@(fontFamily "Arial, Helvetica") uiContainer: term);uiHorizontalScroller: 15 uiAddHorizontalPxSpaces {
			x standardImageView urlPairs
		} each (0 rangeUp length: urlPairs) - 1
}.

$update = () fun {
	scrollersRef = scrollerForTerm: users
}.

$useInput = {
	$users storageSaveString ";" strJoin usersRef=users;currentInput.
	scrollersRef = scrollers;scrollerForTerm:currentInput
}.

usersRef = ";" strSplit storageLoadString: $users.
do:update.

print: @(percentWidth 90) $currentInput uiTextfieldVariableOnchange "".
print: @(percentWidth 10) useInput uiButton "Add".
print: uiShowVariableAsList: scrollersRef.
void
